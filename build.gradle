plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id "com.jfrog.bintray" version "1.8.5"
    id 'maven-publish'
}

repositories {
    mavenLocal()

    jcenter()

    mavenCentral()
}

shadowJar {
    dependencies {
        exclude(dependency('org.jetbrains.kotlin:kotlin-stdlib.*'))
    }
}

defaultTasks("shadowjar")

dependencies {
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
}

group 'me.lovesasuna'
version '1.3.7'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-Xlint:deprecation')
    options.compilerArgs.add('-Xlint:unchecked')
}

java {
    withSourcesJar()
}

bintray {
    user = 'lovesasuna'
    key = System.getenv("JcenterKey")
    publications = ['plugin']
    pkg {
        repo = 'maven'
        name = 'Lanzou'
        licenses = ['AGPL-3.0']
        vcsUrl = 'https://github.com/LovesAsuna/Lanzou.git'
        version {
            name = project.version
            desc = 'Library for Lanzou'
            released = new Date()
        }
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            description 'Library for Lanzou'
            pom {
                developers {
                    developer {
                        name = 'LovesAsuna'
                    }
                }
                licenses {
                    license {
                        name = 'AGPL-3.0'
                        url = 'https://github.com/LovesAsuna/Lanzou/raw/master/LICENSE'
                    }
                }
                scm {
                    url = 'https://github.com/LovesAsuna/Lanzou'
                }
            }
        }
    }
}